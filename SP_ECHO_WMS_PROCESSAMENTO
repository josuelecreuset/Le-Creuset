CREATE OR ALTER PROCEDURE DBO.SP_ECHO_WMS_PROCESSAMENTO(@ID_FILTRO BIGINT = NULL, @PEDIDO_FILTRO VARCHAR(12) = NULL , @EXIBE_PRINT BIT = 0, @RETORNA_STATUS BIT = 0, @BUSCA_STATUS_WMS BIT = 0)
AS
BEGIN 
	SET NOCOUNT ON

	DECLARE @STATUS INT,
			@STATUS_ANTERIOR INT,
			@FILIAL VARCHAR(25),
			@GERA_FATURAMENTO BIT = 0,
			@AGUARDA_PICKUP BIT = 0,
			@PEDIDO VARCHAR(15),
			@ID BIGINT,
			@CLIENTE VARCHAR(25), 
			@INTEGRA_VOLO BIT,
			@NF_SAIDA VARCHAR(15),
			@SERIE_NF VARCHAR(10)

/*
	SELECT 0, 'Linx - Pendente Processamento'	UNION ALL
	SELECT 1, 'Linx - Faturamento Gerado'		UNION ALL
	SELECT 2, 'WMS - Aguardando Despacho'		UNION ALL
	SELECT 3, 'WMS - Aguardando Picking'		UNION ALL
	SELECT 4, 'WMS - Despachado'				UNION ALL
	SELECT 5, 'WMS - Picking Concluido'			UNION ALL
	SELECT 8, 'Historico'						UNION ALL
	SELECT 9, 'Linx	- Erro Integracao'	
*/

		DECLARE CurIntegracaoWMSVolo CURSOR FOR

		SELECT 
			STATUS,
			STATUS_ANTERIOR,
			ID,
			A.FILIAL,
			PEDIDO = CHAVE,
			CASE WHEN IV.VALOR_PROPRIEDADE = 'SIM' THEN '1' ELSE '0' END,
			CASE WHEN ISNULL(GF.VALOR_PROPRIEDADE,'NAO') = 'SIM' THEN '1' ELSE '0' END,
			CASE WHEN ISNULL(GP.VALOR_PROPRIEDADE,'NAO') = 'SIM' THEN '1' ELSE '0' END,
			NF_SAIDA = A.SAIDA_NF,
			SERIE_NF = A.SAIDA_SERIE
		FROM DBO.TB_ECHO_WMS_CONTROLE A
		JOIN DBO.PROP_FILIAIS IV WITH(NOLOCK) ON -- INTEGRA VOLO
			IV.PROPRIEDADE = 'WMS01' AND
			A.FILIAL = IV.FILIAL
		LEFT JOIN DBO.PROP_FILIAIS GF WITH(NOLOCK) ON -- GERA FATURAMENTO
			GF.PROPRIEDADE = 'WMS02' AND
			A.FILIAL = GF.FILIAL
		LEFT JOIN DBO.PROP_FILIAIS GP WITH(NOLOCK) ON -- GERA PICKING
			GP.PROPRIEDADE = 'WMS03' AND
			A.FILIAL = GP.FILIAL
		LEFT JOIN DBO.PROP_FILIAIS GPA WITH(NOLOCK) ON -- GERA PICKING AUTO
			GPA.PROPRIEDADE = 'WMS04' AND		  
			A.FILIAL = GPA.FILIAL		
		WHERE 
			CASE WHEN STATUS = 9 THEN STATUS_ANTERIOR ELSE STATUS END IN(0,1,2,3,5)
			AND IV.VALOR_PROPRIEDADE = 'SIM'
			AND (ID = @ID_FILTRO OR @ID_FILTRO IS NULL)
			AND (A.CHAVE = @PEDIDO_FILTRO OR @PEDIDO_FILTRO IS NULL)
		UNION ALL
		SELECT 
			DISTINCT
			STATUS = 0,
			STATUS_ANTERIOR = 0,
			ID = 0,
			A.FILIAL,
			PEDIDO,
			CASE WHEN IV.VALOR_PROPRIEDADE = 'SIM' THEN '1' ELSE '0' END,
			CASE WHEN ISNULL(GF.VALOR_PROPRIEDADE,'NAO') = 'SIM' THEN '1' ELSE '0' END,
			CASE WHEN ISNULL(GP.VALOR_PROPRIEDADE,'NAO') = 'SIM' THEN '1' ELSE '0' END,
			NF_SAIDA = NULL,
			SERIE_NF =  NULL
		FROM DBO.VENDAS_PROD_EMBALADO A WITH(NOLOCK) 
		LEFT JOIN DBO.TB_ECHO_WMS_CONTROLE B WITH(NOLOCK) ON
			A.FILIAL = B.FILIAL AND
			A.PEDIDO = B.CHAVE
		JOIN DBO.PROP_FILIAIS IV WITH(NOLOCK) ON -- INTEGRA VOLO
			IV.PROPRIEDADE = 'WMS01' AND
			A.FILIAL = IV.FILIAL
		LEFT JOIN DBO.PROP_FILIAIS GF WITH(NOLOCK) ON -- GERA FATURAMENTO
			GF.PROPRIEDADE = 'WMS02' AND
			A.FILIAL = GF.FILIAL
		LEFT JOIN DBO.PROP_FILIAIS GP WITH(NOLOCK) ON -- GERA PICKING
			GP.PROPRIEDADE = 'WMS03' AND
			A.FILIAL = GP.FILIAL
		LEFT JOIN DBO.PROP_FILIAIS GPA WITH(NOLOCK) ON -- GERA PICKING AUTO
			GPA.PROPRIEDADE = 'WMS04' AND		  
			A.FILIAL = GPA.FILIAL
		WHERE CAIXA_FECHADA = 1
		AND @ID_FILTRO IS NULL
		AND B.ID IS NULL
		AND (A.PEDIDO = @PEDIDO_FILTRO OR @PEDIDO_FILTRO IS NULL)
		AND IV.VALOR_PROPRIEDADE = 'SIM'
		AND (CASE WHEN @PEDIDO_FILTRO IS NULL AND (ISNULL(GPA.VALOR_PROPRIEDADE,'NAO') = 'SIM' OR ISNULL(GF.VALOR_PROPRIEDADE,'NAO') = 'SIM') THEN 1 ELSE 0 END = 1 OR CASE WHEN @PEDIDO_FILTRO IS NOT NULL AND (ISNULL(GP.VALOR_PROPRIEDADE,'NAO') = 'SIM' OR ISNULL(GF.VALOR_PROPRIEDADE,'NAO') = 'SIM') THEN 1 ELSE 0 END = 1 )
	
		OPEN CurIntegracaoWMSVolo;
 
		FETCH NEXT FROM CurIntegracaoWMSVolo
		INTO @STATUS,
			 @STATUS_ANTERIOR,
			 @ID,
			 @FILIAL,
			 @PEDIDO,
			 @INTEGRA_VOLO,
			 @GERA_FATURAMENTO,
			 @AGUARDA_PICKUP,
			 @NF_SAIDA ,
			 @SERIE_NF;
 
		WHILE @@FETCH_STATUS = 0
		BEGIN
			
			IF @EXIBE_PRINT = 1 PRINT('Filial: '+@FILIAL)
			IF @EXIBE_PRINT = 1 PRINT('Pedido: '+@PEDIDO)
			IF @EXIBE_PRINT = 1 PRINT('STATUS  = '+CONVERT(VARCHAR(10),@STATUS))
			IF @EXIBE_PRINT = 1 PRINT('ID  = '+CONVERT(VARCHAR(10),ISNULL(@ID,0)))

			IF @INTEGRA_VOLO = 1
			BEGIN
				IF @EXIBE_PRINT = 1 PRINT('Integra Volo  1')
			
				SET @STATUS = CASE WHEN @STATUS = 9 THEN @STATUS_ANTERIOR ELSE @STATUS END
				
				IF @ID = 0
				SET @STATUS = 0
				
				IF @EXIBE_PRINT = 1 PRINT('STATUS  = '+CONVERT(VARCHAR(10),@STATUS))

				IF @STATUS = 0 -- Linx - Pendente Processamento
				BEGIN
					IF @EXIBE_PRINT = 1 PRINT('STATUS 0')

					IF @EXIBE_PRINT = 1 PRINT('@GERA_FATURAMENTO ='+convert(varchar(10),@GERA_FATURAMENTO))
					IF @EXIBE_PRINT = 1 PRINT('@AGUARDA_PICKUP ='+convert(varchar(10),@AGUARDA_PICKUP))


					IF @GERA_FATURAMENTO = 1 AND @AGUARDA_PICKUP = 0 -- ECOM INICIALMENTE SÓ
					BEGIN
						IF @EXIBE_PRINT = 1 PRINT('CHAMA SP_ECHO_WMS_GERA_FATURAMENTO')
						EXEC DBO.SP_ECHO_WMS_GERA_FATURAMENTO @PEDIDO, @EXIBE_PRINT, @RETORNA_STATUS,@STATUS,1
					END

					IF @GERA_FATURAMENTO = 0 AND @AGUARDA_PICKUP = 1 -- ECOM INICIALMENTE SÓ
					BEGIN
						IF @EXIBE_PRINT = 1 PRINT('CHAMA SP_ECHO_WMS_ENVIA_PICKING')
						EXEC DBO.SP_ECHO_WMS_ENVIA_PICKING @PEDIDO, @EXIBE_PRINT,@RETORNA_STATUS
					END

				END

				IF @STATUS = 1 -- Linx - Faturamento Gerado
				BEGIN
					IF @EXIBE_PRINT = 1 PRINT(@STATUS)

					IF @EXIBE_PRINT = 1 PRINT('CHAMA SP_ECHO_WMS_GERA_TRANSFERENCIA')
					EXEC DBO.SP_ECHO_WMS_GERA_TRANSFERENCIA @ID, @EXIBE_PRINT, 0

					IF @EXIBE_PRINT = 1 PRINT('CHAMA SP_ECHO_WMS_ENVIA_FATURAMENTO')
					EXEC DBO.SP_ECHO_WMS_ENVIA_FATURAMENTO  @ID, @EXIBE_PRINT, @RETORNA_STATUS
				END		
				
				IF @STATUS = 2 -- WMS - Aguardando Despacho
				BEGIN
					IF @EXIBE_PRINT = 1 PRINT(@STATUS)
					IF @EXIBE_PRINT = 1 PRINT('Busca Stage Volo Atualizacao - Aguardando Despacho')

					IF(SELECT COUNT(*) FROM [VOLO].DBO.IT4_WMS_DOCUMENTO WITH(NOLOCK) WHERE Documento = @PEDIDO AND DATA_EXPEDICAO IS NOT NULL) > 0 -- Criar indice conforme este where 
					BEGIN
						UPDATE 
							TB_ECHO_WMS_CONTROLE 
						SET STATUS_ANTERIOR = STATUS,
							STATUS = '4' -- MARCA STATUS COMO DESPACHADO
							, DATA_DESPACHADO = CONVERT(DATE, GETDATE())
						WHERE ID = @ID		
					END

				END

				IF @STATUS = 3 -- WMS - Aguardando Picking
				BEGIN
					IF @EXIBE_PRINT = 1 PRINT(@STATUS)
					IF @EXIBE_PRINT = 1 PRINT('Busca Stage Volo Atualizacao - Aguardando Picking')

					IF(SELECT COUNT(*) FROM [VOLO].DBO.IT4_WMS_DOCUMENTO WITH(NOLOCK) WHERE Documento = @PEDIDO AND Retorno IS NOT NULL) > 0 -- Criar indice conforme este where 
					BEGIN
						UPDATE 
							TB_ECHO_WMS_CONTROLE 
						SET STATUS_ANTERIOR = STATUS,
							STATUS = '5', -- MARCA STATUS COMO DESPACHADO
							DATA_PICKING_CONCLUIDO = CONVERT(DATE, GETDATE())
						WHERE ID = @ID		

					END
				END

				IF @STATUS = 5 -- WMS - Picking Concluido
				BEGIN
					IF ((@GERA_FATURAMENTO = 1 AND @ID_FILTRO IS NULL) OR (@ID_FILTRO is not null and @GERA_FATURAMENTO = 0))
					BEGIN
						IF @EXIBE_PRINT = 1 PRINT(@STATUS)
						IF @EXIBE_PRINT = 1 PRINT('Gera Faturamento - Picking Concluido')

						DECLARE @VOLUMES INT = 0

						SELECT @VOLUMES = Volumes FROM [VOLO].DBO.IT4_WMS_DOCUMENTO WITH(NOLOCK) WHERE Documento = @PEDIDO AND Retorno IS NOT NULL

						EXEC DBO.SP_ECHO_WMS_GERA_FATURAMENTO @PEDIDO, @EXIBE_PRINT, @RETORNA_STATUS, @STATUS, @VOLUMES
					END
				END

			END
		
		FETCH NEXT FROM CurIntegracaoWMSVolo
		INTO @STATUS,
			 @STATUS_ANTERIOR,
			 @ID,
			 @FILIAL,
			 @PEDIDO,
			 @INTEGRA_VOLO,
			 @GERA_FATURAMENTO,
			 @AGUARDA_PICKUP,
			 @NF_SAIDA,
			 @SERIE_NF;
		END
 
		CLOSE CurIntegracaoWMSVolo;
		DEALLOCATE CurIntegracaoWMSVolo;


END
