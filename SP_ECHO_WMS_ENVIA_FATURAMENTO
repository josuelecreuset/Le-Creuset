CREATE OR ALTER PROCEDURE DBO.SP_ECHO_WMS_ENVIA_FATURAMENTO(@ID BIGINT, @EXIBE_PRINT BIT = 0, @RETORNA_STATUS BIT = 0)
AS
BEGIN 
	SET NOCOUNT ON

	DECLARE @NF_SAIDA			VARCHAR(15),
			@SERIE_NF			VARCHAR(6),
			@FILIAL				VARCHAR(25),
			@NOME_CLIFOR		VARCHAR(25),
			@CGC_CPF_CLI		VARCHAR(14),
			@PEDIDO				VARCHAR(15),
			@STATUS				INT,
			@STEP				INT,
			@ID_VOLO			BIGINT,
			@EMISSAO			DATETIME,
			@COD_CLIFOR			VARCHAR(6),
			@CHAVE_NFE			VARCHAR(50),
			@STATUS_CONCLUSAO	INT,
			@MSG_CONCLUSAO		VARCHAR(254),
			@XML_NFE			NVARCHAR(MAX),
			@CNPJ_ORIGINAL		VARCHAR(14),
			@TROCAR_CNPJ		BIT = 0,
			@CNPJ_TROCAR		VARCHAR(14)

-- Se existir, Drop tabelas Temporarias
	IF OBJECT_ID('TempDB.dbo.#TMP_XML_LINX') IS NOT NULL DROP TABLE #TMP_XML_LINX
	CREATE TABLE #TMP_XML_LINX(XML_NFE XML, TAMANHO_XML VARCHAR(20), CHAVE_NFE VARCHAR(100), EMAIL_NFE VARCHAR(MAX), EMAIL_DEST VARCHAR(MAX))

	SELECT 
		@PEDIDO		= CHAVE,
		@NF_SAIDA	= SAIDA_NF,
		@SERIE_NF	= SAIDA_SERIE,
		@STATUS		= STATUS,
		@FILIAL		= FILIAL
	FROM DBO.TB_ECHO_WMS_CONTROLE WITH(NOLOCK) 
	WHERE ID = @ID

	IF @STATUS IN(1,5)
	BEGIN
		
		BEGIN TRY
			IF @EXIBE_PRINT = 1 PRINT('STATUS '+CONVERT(VARCHAR(10),@STATUS))
			IF @EXIBE_PRINT = 1 PRINT('Envia Faturamento')
			
			SELECT 
				@NOME_CLIFOR = NOME_CLIFOR,
				@EMISSAO	 = EMISSAO,
				@CHAVE_NFE   = CHAVE_NFE
 			FROM FATURAMENTO WITH(NOLOCK) 
			WHERE	FILIAL = @FILIAL AND 
					NF_SAIDA = @NF_SAIDA AND 
					SERIE_NF = @SERIE_NF 
			
			SELECT @COD_CLIFOR = COD_CLIFOR, @CGC_CPF_CLI = CGC_CPF FROM CADASTRO_CLI_FOR WITH(NOLOCK) WHERE NOME_CLIFOR = @NOME_CLIFOR

			IF (SELECT COUNT(*) FROM DBO.PROP_FILIAIS WITH(NOLOCK) WHERE FILIAL = @FILIAL AND PROPRIEDADE = 'WMS06' AND VALOR_PROPRIEDADE = 'SIM') > 0 
			BEGIN
				SELECT @CNPJ_ORIGINAL = CGC_CPF FROM DBO.FILIAIS WITH(NOLOCK) WHERE FILIAL = @FILIAL
				SET @TROCAR_CNPJ = 1
				SELECT @CNPJ_TROCAR = RTRIM(LTRIM(VALOR_ATUAL)) FROM DBO.PARAMETROS WITH(NOLOCK) WHERE PARAMETRO  = 'WMS_CNPJ_STAGE_VOLO'
			END

			BEGIN TRAN
			
			INSERT INTO #TMP_XML_LINX
			EXEC DBO.LX_GERA_NFE_SEFAZ_4_00
				@NOTAFISCAL		= @NF_SAIDA,
				@SERIENOTA		= @SERIE_NF,
				@FILIALNOTA		= @FILIAL,
				@RETORNA_XML	= 1
			
			SELECT 
				@XML_NFE = REPLACE(CONVERT(NVARCHAR(MAX),XML_NFE),'<emit><CNPJ>'+RTRIM(LTRIM(@CNPJ_ORIGINAL))+'</CNPJ>','<emit><CNPJ>'+RTRIM(LTRIM(@CNPJ_TROCAR))+'</CNPJ>')
			FROM #TMP_XML_LINX	
			

			
			IF @STATUS = 1 AND (SELECT COUNT(*) FROM  VOLO.DBO.IT4_WMS_DOCUMENTO WITH(NOLOCK) WHERE Documento = @PEDIDO ) = 0
			BEGIN
				SET @STEP = 1
				INSERT INTO
					[VOLO].DBO.IT4_WMS_DOCUMENTO
					(Documento,
					Serie,
					Filial,
					Cliente_Ou_Fornecedor,
					Data,
					Origem,
					TIPO,
					Enviado,
					Retorno,
					Caixa,
					Cancelamento,
					Cancelado,
					Peso,
					Volumes,
					msg_error,
					CHAVE_NFE,
					DATA_ENVIO_FATURAMENTO,
					TICKET,
					XML_FATURAMENTO,
					TICKET_FATURAMENTO,
					CGC_CPF,
					NF_SAIDA,
					romaneio_entrada,
					CAIXAS_VOLO,
					DATA_EXPEDICAO,
					CNPJ_ORIGINAL)
				SELECT 
					Documento					= @PEDIDO,
					Serie						= '999',
					Filial						= @FILIAL,
					Cliente_Ou_Fornecedor		= @COD_CLIFOR,
					Data						= @EMISSAO,
					Origem						= 'P',		-- FIXO
					TIPO						= 'NORM',	-- FIXO
					Enviado						= NULL,		-- NULLABLE
					Retorno						= NULL,		-- NULLABLE
					Caixa						= NULL,		-- NULLABLE
					Cancelamento				= NULL,		-- NULLABLE
					Cancelado					= NULL,		-- NULLABLE
					Peso						= NULL,		-- NULLABLE
					Volumes						= NULL,		-- NULLABLE
					msg_error					= NULL,		-- NULLABLE
					CHAVE_NFE					= @CHAVE_NFE,		-- NULLABLE
					DATA_ENVIO_FATURAMENTO		= GETDATE(),		-- NULLABLE
					TICKET						= NULL,		-- NULLABLE
					XML_FATURAMENTO				= @XML_NFE,		-- NULLABLE
					TICKET_FATURAMENTO			= NULL,		-- NULLABLE
					CGC_CPF						= @CGC_CPF_CLI,		-- NULLABLE
					NF_SAIDA					= @NF_SAIDA,		-- NULLABLE
					romaneio_entrada			= NULL,		-- NULLABLE
					CAIXAS_VOLO					= NULL,		-- NULLABLE
					DATA_EXPEDICAO				= NULL,		-- NULLABLE
					CNPJ_ORIGINAL				= @CNPJ_ORIGINAL
			
				SET @ID_VOLO = @@IDENTITY

				SET @STEP = 2
				INSERT INTO
					[VOLO].DBO.IT4_WMS_DOCUMENTO_ITEM
					(idControle,
					IdItem,
					Codigo_Barra,
					Descricao,
					Qtde,
					QtdeRetorno,
					IDLote,
					TICKET)
				SELECT
					idControle = @ID_VOLO,
					IdItem = VP.SEQUENCIAL_DIGITACAO,
					Codigo_Barra = B.CODIGO_BARRA,
					Descricao = P.DESC_PROD_NF,
					Qtde = A.QTDE,
					QtdeRetorno = NULL,
					IDLote = NULL,
					TICKET = NULL
				FROM DBO.FATURAMENTO_PROD A WITH(NOLOCK) 
				JOIN DBO.VENDAS_PRODUTO VP WITH(NOLOCK) ON
					A.PEDIDO = VP.PEDIDO AND
					A.PRODUTO = VP.PRODUTO AND
					A.COR_PRODUTO = VP.COR_PRODUTO AND
					A.ENTREGA = VP.ENTREGA AND
					A.ITEM_PEDIDO = VP.ITEM_PEDIDO
				JOIN PRODUTOS_BARRA B WITH(NOLOCK) ON
					A.PRODUTO = B.PRODUTO AND
					A.COR_PRODUTO = B.COR_PRODUTO AND
					B.CODIGO_BARRA_PADRAO  = 1 AND 
					B.TIPO_COD_BAR = 1 
					AND B.INATIVO = 0 
				JOIN PRODUTOS P WITH(NOLOCK) ON
					A.PRODUTO = P.PRODUTO
				WHERE A.FILIAL = @FILIAL AND A.NF_SAIDA =@NF_SAIDA AND A.SERIE_NF = @SERIE_NF
			END 
			ELSE
			BEGIN
				UPDATE IT4_WMS_DOCUMENTO
						SET CHAVE_NFE = @CHAVE_NFE,
						DATA_ENVIO_FATURAMENTO		= GETDATE(),
						XML_FATURAMENTO				= @XML_NFE,
						NF_SAIDA					= @NF_SAIDA
				FROM [VOLO].DBO.IT4_WMS_DOCUMENTO
				WHERE Documento = @PEDIDO
			END

			UPDATE DBO.TB_ECHO_WMS_CONTROLE 
				SET  STATUS_ANTERIOR	= STATUS
					,STATUS				= 2
					,DATA_HORA_UPDATE	= GETDATE()
					,USUARIO			= SUSER_NAME()
					,APP_EXEC			= APP_NAME()
					,HOST				= HOST_NAME()
			WHERE ID = @ID


			SET @STATUS_CONCLUSAO = 2
			SET @MSG_CONCLUSAO = 'SUCESSO'

			INSERT INTO 
				DBO.TB_ECHO_WMS_CONTROLE_STATUS
				(ID,
				CHAVE,
				STATUS,
				DATA_HORA)
			SELECT 
				ID = @ID,
				CHAVE = @PEDIDO,
				STATUS = 2,
				DATA_HORA = GETDATE()

			COMMIT TRAN


		END TRY
		BEGIN CATCH

			DECLARE @ERRO_EXCEPTION VARCHAR(254)
			SET @ERRO_EXCEPTION = ERROR_MESSAGE()
			
			IF @EXIBE_PRINT = 1  PRINT('Erro Step: '+CONVERT(VARCHAR(2),@STEP)+' - '+@ERRO_EXCEPTION)

			ROLLBACK TRAN
			
			INSERT INTO
				DBO.TB_ECHO_WMS_CONTROLE_LOGS
				(ID,
				 ITEM,
				 ORIGEM,
				 DETALHES)
			SELECT
				 ID = @ID,
				 ITEM = 1,
				 ORIGEM = 'SP_ECHO_WMS_ENVIA_FATURAMENTO',
				 DETALHES = 'Step: '+CONVERT(VARCHAR(2),@STEP)+' - '+@ERRO_EXCEPTION
		
			UPDATE TB_ECHO_WMS_CONTROLE
				SET STATUS_ANTERIOR		= CASE WHEN STATUS = 9 THEN STATUS_ANTERIOR ELSE STATUS END,
					STATUS				= 9,
					DATA_HORA_UPDATE	= GETDATE(),
					USUARIO				= SUSER_NAME(),
					HOST				= HOST_NAME(),
					APP_EXEC			= APP_NAME()
			WHERE ID = @ID

			SET @STATUS_CONCLUSAO = 9
			SET @MSG_CONCLUSAO = @ERRO_EXCEPTION

			INSERT INTO 
				DBO.TB_ECHO_WMS_CONTROLE_STATUS
				(ID,
				CHAVE,
				STATUS,
				DATA_HORA)
			SELECT 
				ID = @ID,
				CHAVE = @PEDIDO,
				STATUS = 9,
				DATA_HORA = GETDATE()
		END CATCH


	END


	IF @RETORNA_STATUS = 1
	SELECT STATUS = @STATUS_CONCLUSAO,	MENSAGEM = @MSG_CONCLUSAO	

END
